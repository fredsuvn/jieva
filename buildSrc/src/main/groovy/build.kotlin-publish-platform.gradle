plugins {
  id 'build.core'
  id 'maven-publish'
  id 'java-platform'
  id 'signing'
}

printer.info("${project.name}: import kotlin-publish-platform")

if (infos.publish.isPublish) {
  publishing {
    publications {
      maven(MavenPublication) {
        from components.javaPlatform

        pom {
          name = project.name
          description = project.description
          url = infos.url
          inceptionYear = infos.inceptionYear
          licenses {
            infos.licenses.forEach { each ->
              license {
                name = each.name
                url = each.url
              }
            }
          }
          scm {
            connection = "scm:git:${infos.url}.git"
            developerConnection = "scm:git:${infos.url}.git"
            url = infos.url
          }
          developers {
            infos.developers.forEach { each ->
              developer {
                email = each.email
              }
            }
          }
        }
      }
    }

    repositories {
      mavenLocal()
      if (infos.publish.isSnapshot) {
        maven {
          name = "${infos.publish.snapshotId}"
          url = infos.publish.urls.snapshot
          credentials {
            username = "${passwords."${infos.publish.snapshotId}".username}"
            password = "${passwords."${infos.publish.snapshotId}".password}"
          }
        }
      }
      if (infos.publish.isRelease) {
        maven {
          name = "${infos.publish.releaseId}"
          url = infos.publish.urls.staging
          credentials {
            username = "${passwords."${infos.publish.releaseId}".username}"
            password = "${passwords."${infos.publish.releaseId}".password}"
          }
        }
      }
    }

    if (infos.publish.isSigning) {
      ext."signing.keyId" = passwords."${infos.publish.signingId}".keyId
      ext."signing.password" = passwords."${infos.publish.signingId}".password
      ext."signing.secretKeyRingFile" = passwords."${infos.publish.signingId}".secretKeyRingFile
      signing {
        sign publishing.publications.maven
      }
    }
  }
}