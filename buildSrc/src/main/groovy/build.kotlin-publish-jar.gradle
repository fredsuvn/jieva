plugins {
  id 'build.core'
  id 'build.kotlin-compile'
  id 'org.jetbrains.dokka'
  //id "org.jetbrains.kotlin.kapt"
  id 'maven-publish'
  id 'signing'
}

printer.info("${project.name}: import kotlin-publish-jar")

task sourceJar(type: Jar) {
  classifier "sources"
  from sourceSets.main.allSource
}

//task javadocJar(type: Jar, dependsOn: javadoc) {
//  classifier "javadoc"
//  from javadoc.destinationDir
//}
task javadocJar(type: Jar, dependsOn: dokkaJavadoc {
  outputDirectory = javadoc.destinationDir
}) {
  classifier "javadoc"
  from javadoc.destinationDir
}

artifacts {
  archives jar
  archives sourceJar
  archives javadocJar
}

//compileKotlin.inputs.files(processResources)
//compileJava.inputs.files(processResources)

if (infos.publish.enablePublish) {
  publishing {
    publications {
      maven(MavenPublication) {
        from components.java

        artifact(sourceJar) {
          classifier = 'sources'
        }

        artifact(javadocJar) {
          classifier = 'javadoc'
        }

        pom {
          name = project.name
          description = project.description
          url = infos.url
          inceptionYear = infos.inceptionYear
          scm {
            connection = "scm:git:${infos.url}.git"
            developerConnection = "scm:git:${infos.url}.git"
            url = infos.url
          }
          if (infos.licenses != null) {
            licenses {
              infos.licenses.forEach { each ->
                license {
                  name = each.name
                  url = each.url
                }
              }
            }
          }
          if (infos.developers != null) {
            developers {
              infos.developers.forEach { each ->
                developer {
                  email = each.email
                }
              }
            }
          }
        }
      }
    }

    repositories {
      mavenLocal()
      if (infos.publish.isSnapshot) {
        maven {
          name = infos.publish.snapshotId
          url = infos.publish.urls.snapshot
          credentials {
            username = getProperty("publish${name.capitalize()}Username")
            password = getProperty("publish${name.capitalize()}Password")
            printer.debug("snapshot.username: $username")
            printer.debug("snapshot.password: $password")
          }
        }
      }
      if (infos.publish.isRelease) {
        maven {
          name = infos.publish.releaseId
          url = infos.publish.urls.release
          credentials {
            username = getProperty("publish${name.capitalize()}Username")
            password = getProperty("publish${name.capitalize()}Password")
            printer.debug("release.username: $username")
            printer.debug("release.password: $password")
          }
        }
      }
    }

    if (infos.publish.isSigning) {
      signing {
        def signingName = infos.publish.signingId
        def signingKeyId = getProperty("signing${signingName.capitalize()}KeyId")
        def signingPassword = getProperty("signing${signingName.capitalize()}Password")
        def signingKey = utils.readFileAsString(getProperty("signing${signingName.capitalize()}KeyFile"), "UTF-8")
        printer.debug("signing.signingKeyId: $signingKeyId")
        printer.debug("signing.signingPassword: $signingPassword")
        printer.debug("signing.signingKey: $signingKey")
        useInMemoryPgpKeys(signingKeyId, signingKey, signingPassword)
        sign publishing.publications.maven
      }
    }
  }
}